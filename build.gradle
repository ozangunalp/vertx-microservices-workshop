buildscript {
    repositories { mavenLocal() }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.3-SNAPSHOT'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
//    id "se.transmode.gradle.docker" version "1.3-SNAPSHOT"
}

def mainClass = 'io.vertx.core.Launcher'
def codeGenProcessor = 'io.vertx.codegen.CodeGenProcessor'
def dockerGroupName = 'vertx-microservice-workshop'
def dockerServiceType = 'unknown'

allprojects {
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    group = 'io.vertx.workshop'
    version = '1.0-SNAPSHOT'
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'docker'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def generateSrcPath = "$projectDir/src/main/generated"

    ext.dockerServiceType = dockerServiceType

    task generateServiceProxy(type: JavaCompile, description: 'Generates ServiceProxies') {
        source = sourceSets.main.java
        classpath = configurations.compile
        destinationDir = file(generateSrcPath)
        options.compilerArgs = ["-proc:only",
                                "-processor", codeGenProcessor,
                                "-AoutputDirectory=$generateSrcPath"
        ]
    }
    compileJava.dependsOn generateServiceProxy

    shadowJar {
        mergeServiceFiles()
        manifest {
            attributes(
                    'Description': 'This is an application JAR',
                    'Main-Class': mainClass,
            )
        }
        classifier 'fat'
        dependsOn build
    }

    docker {
        maintainer = 'Ozan Gunalp <ozangunalp@gmail.com>'
        baseImage = 'java:8'
    }

    task dockerBuild(type: Docker) {
        group = dockerGroupName
        applicationName = project.name
        tag = "$dockerGroupName/$applicationName"
        tagVersion = project.version
        dockerfile {
            expose 8080
            workdir '/opt'
            env 'SERVICE_TYPE', {
                project.dockerServiceType
            }
            run 'chmod -R 777 /opt'
            add {
                from shadowJar.outputs
                from "$projectDir/src/docker"
                into '/opt'
            }
            entryPoint "java -jar ${project.name}-${project.version}-fat.jar -cluster -conf config.json -cp ."
        }
        doLast {
            println dockerfile.instructions
        }
        dependsOn shadowJar
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java', 'src/main/generated'
            }
        }
    }

    task packageSources(type: Jar) {
        classifier = 'sources'
        includeEmptyDirs = false
        from sourceSets.main.java, sourceSets.main.groovy, sourceSets.main.resources
    }

    task packageTests(type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    artifacts.archives packageSources
    artifacts.archives packageTests

    repositories {
        mavenLocal()

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    configurations.all {
    }

    dependencies {
        compile group: 'io.vertx', name: 'vertx-core', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-service-proxy', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-lang-groovy', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-lang-js', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-lang-ruby', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-codegen', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-sockjs-service-proxy', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-web', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-hazelcast', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-service-discovery', version: '3.3.0.CR1'
        compile group: 'io.vertx', name: 'vertx-service-discovery-bridge-docker-links', version: '3.3.0.CR1'

        testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.3.0.CR1'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.3.0'
        testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
        testCompile group: 'com.jayway.restassured', name: 'rest-assured', version: '2.8.0'
    }


}
